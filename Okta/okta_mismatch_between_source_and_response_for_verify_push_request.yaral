rule okta_mismatch_between_source_and_response_for_verify_push_request
{
  meta:
     subject = "okta mismatch between source and response for verify push request"
     description = "Okta Mismatch Between Source and Response for Verify Push Request"
     tactic = "Credential Access"
     technique = "Multi-Factor Authentication Interception"
     product = "Okta"
     logsource = "SSO"
     author = "Jason Walker @tresscross"
     confidence = "Medium"
     severity = "Low"

  events:
    $push.metadata.log_type = "OKTA"
    $push.metadata.event_type = "USER_UNCATEGORIZED"
    $push.metadata.product_event_type = "system.push.send_factor_verify_push"
    $push.security_result.description = /New Device=POSITIVE/
    $push.security_result.description = /New IP=POSITIVE/
    $push.security_result.action = "ALLOW"
    $push.principal.ip = $push_ip
    $push.network.parent_session_id = $parent_session_id

    $auth.metadata.log_type = "OKTA"
    $auth.metadata.event_type = "USER_LOGIN"
    $auth.metadata.product_event_type = "user.authentication.auth_via_mfa"
    $auth.security_result.detection_fields["factor"] = "OKTA_VERIFY_PUSH"
    $auth.security_result.action = "ALLOW"
    $auth.principal.ip = $auth_ip
    $auth.network.parent_session_id = $parent_session_id

    $push_ip != $auth_ip
    $push.metadata.event_timestamp.seconds <= $auth.metadata.event_timestamp.seconds

  match:
    $parent_session_id over 5m

  outcome:
    $risk_score = max(
        // Baseline
        35 +
        // If the IP Address is marked as suspicious IP address by Okta ThreatInsight
        if($push.security_result.detection_fields["threatSuspected"] = "true", 30) +
        // Based on Okta Behavior Detection pattern analysis
        if($push.security_result.description = /New Geo-Location=POSITIVE/, 10) +
        if($push.security_result.description = /New Device=POSITIVE/, 20) +
        if($push.security_result.description = /New IP=POSITIVE/, 5) +
        if($push.security_result.description = /New City=POSITIVE/, 5) +
        if($push.security_result.description = /New State=POSITIVE/, 10) +
        if($push.security_result.description = /New Country=POSITIVE/, 10) +
        if($push.security_result.description = /Velocity=POSITIVE/, 10) +
        // Unauthorized target geographies
        if($push.target.ip_geo_artifact.location.country_or_region = "Cuba", 20) +
        if($push.target.ip_geo_artifact.location.country_or_region = "Iran", 20) +
        if($push.target.ip_geo_artifact.location.country_or_region = "North Korea", 20) +
        if($push.target.ip_geo_artifact.location.country_or_region = "Russia", 20) +
        if($push.target.ip_geo_artifact.location.country_or_region = "Syria", 20)
    )
    $push_principal_ip_country = array_distinct($push.principal.ip_geo_artifact.location.country_or_region)
    $push_principal_ip_state = array_distinct($push.principal.ip_geo_artifact.location.state)
    $push_principal_ip_city = array_distinct($push.principal.location.city)
    $push_principal_user_email_addresses = array_distinct ($push.principal.user.email_addresses)
    $push_security_result_summary = array_distinct($push.security_result.summary)
    $push_target_user_email_addresses = array_distinct($push.target.user.email_addresses)
    $push_target_user_userid = array_distinct($push.target.user.userid)
    $auth_target_user_agent = array_distinct($auth.network.http.user_agent)
    $auth_principal_ip = array_distinct($auth.principal.ip)
    $auth_principal_ip_country = array_distinct($auth.principal.ip_geo_artifact.location.country_or_region)
    $auth_principal_ip_state = array_distinct($auth.principal.ip_geo_artifact.location.state)
    $auth_principal_ip_city = array_distinct($auth.principal.location.city)
    $auth_principal_user_email_addresses = array_distinct ($auth.principal.user.email_addresses)
    $auth_security_result_summary = array_distinct($auth.security_result.summary)
    $auth_target_user_email_addresses = array_distinct($auth.target.user.email_addresses)
    $auth_target_user_userid = array_distinct($auth.target.user.userid)

  condition:
    $push and $auth
}
