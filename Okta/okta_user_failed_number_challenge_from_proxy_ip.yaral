rule okta_user_failed_number_challenge_from_proxy_ip
{
  meta:
    author = "Jason Walker @tresscross"
    description = "Detects when an Okta user failed a number challenge during push notification."
    reference = "https://support.okta.com/help/s/article/Number-Challenge-for-Okta-Verify"
    tactic = "Credential Access"
    technique = "Multi-Factor Authentication Interception"
    type = "Alert"
    data_source = "Okta"
    severity = "Medium"
    priority = "Medium"

  events:
    $login.metadata.log_type = "OKTA"
    $login.metadata.event_type = "USER_LOGIN"
    $login.metadata.product_event_type = "user.authentication.auth_via_mfa"
    $login.security_result.detection_fields["pushWithNumberChallengeResponseType"] = "OV_WITH_CHALLENGE_RESPONSE_INVALID"
    $login.security_result.action = "BLOCK"
    $login.target.user.userid = $userid
    $login.security_result.detection_fields["anonymized IP"] = "true" nocase

  match:
    $userid over 30m

  outcome:
    $target_user_agent = array_distinct($login.network.http.user_agent)
    $principal_ip = array_distinct($login.principal.ip)
    $principal_ip_country = array_distinct($login.principal.ip_geo_artifact.location.country_or_region)
    $principal_ip_state = array_distinct($login.principal.ip_geo_artifact.location.state)
    $principal_ip_city = array_distinct($login.principal.location.city)
    $principal_user_email_addresses = array_distinct ($login.principal.user.email_addresses)
    $security_result_summary = array_distinct($login.security_result.summary)
    $target_user_email_addresses = array_distinct($login.target.user.email_addresses)
    $target_user_userid = array_distinct($login.target.user.userid)
    $principal_ip_carrier = array_distinct($login.principal.network.carrier_name)

  condition:
    $login
}
