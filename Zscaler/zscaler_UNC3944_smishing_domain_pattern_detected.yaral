rule zscaler_UNC3944_smishing_domain_pattern_detected {

  meta:
    author = "Jason Walker @tresscross"
    description = "UNC3944 frequently hosts their phishing kit on domains with the following patterns."
    reference = "https://cloud.google.com/blog/topics/threat-intelligence/unc3944-sms-phishing-sim-swapping-ransomware/"
    severity = "High"
    tactic = "Initial Access"
    technique = "Phishing"
    subtechnique = "Spearphishing via Service"
    tool = "ZScaler"

  events:
    $base_domain = strings.extract_domain($e.target.url)
    (
        $base_domain = /.*-sso.com/
        OR $base_domain = /.*-sso.net/
        OR $base_domain = /sso\..*.com/
        OR $base_domain = /sso-.*.com/
        OR $base_domain = /.*sso.com.com/
        OR $base_domain = /.*sso.com.net/
        OR $base_domain = /.*-help.com/
        OR $base_domain = /.*-helpdesk.com/
        OR $base_domain = /.*-servicenow.com/
        OR $base_domain = /.*-servicenow.net/
        OR $base_domain = /servicenow-.*.com/
        OR $base_domain = /.*-internal.com/
        OR $base_domain = /.*-schedule.ca/
        OR $base_domain = /.*-schedule.com/
    )
    $e.metadata.log_type = "ZSCALER_WEBPROXY"

    $base_domain != "microsoftazuread-sso.com" //common legitimate MS domain that matches the above pattern

    $e.principal.user.userid = $user

  match:
    $user over 5m

  outcome:
    $original_domain = array_distinct(strings.extract_domain($e.target.url))
    $source = array_distinct($e.principal.ip)
    $destination = array_distinct($e.target.url)
    $destinationip = array_distinct($e.target.ip)
    $target_ip_country = array_distinct($e.target.ip_geo_artifact.location.country_or_region)
    $target_ip_state = array_distinct($e.target.ip_geo_artifact.location.state)
    $target_ip_city = array_distinct($e.target.ip_geo_artifact.location.city)
    $target_ip_carrier = array_distinct($e.target.ip_geo_artifact.network.carrier_name)
    $p_user = array_distinct($e.principal.user.userid)

  condition:
    $e
}
