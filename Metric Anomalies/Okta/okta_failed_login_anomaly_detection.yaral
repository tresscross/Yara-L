rule okta_failed_login_anomaly_detection {
  meta:
    author = "Jason Walker @tresscross"
    description = "Detect highly anomalous failed user login behavior via 30 day behavior analysis"
    tactic = "Credential Access"
    technique = "Valid Accounts"
    severity = "Medium"
    priority = "Medium"

  events:
    $e.metadata.event_type = "USER_LOGIN"
    $e.security_result.action != "ALLOW"
    $e.target.user.userid = $user
    $e.metadata.log_type = "OKTA"
    $e.security_result.summary = "User login to Okta" 

  match:
    $user over 1d

  outcome:
    $event_count_days_seen = max(metrics.auth_attempts_total( //counts the number days with login events in last 30
        period:1d, window:30d,
        metric:event_count_sum,
        agg:num_metric_periods,
        target.user.userid: $user
    ))
    $total_failed_logins = max(metrics.auth_attempts_fail( //counts the total number of failed logins
        period:1d, window:30d,
        metric:event_count_sum,
        agg:sum,
        target.user.userid: $user
    ))

    $stddev_event_count_window = max(metrics.auth_attempts_fail( //calculates the stddev of todays login count against 30 day statistics
        period:1d, window:30d,
        metric:event_count_sum, 
        agg:stddev,
        target.user.userid: $user
    ))

    $current_count = count_distinct($e.metadata.id) //number of login events today

    $baseline_failed_logins = $total_failed_logins / $event_count_days_seen
    $anomaly_score = $current_count / $baseline_failed_logins 
    //calculates how many times over today's login count is over the 30 day baseline

    $t_user = array_distinct($e.target.user.userid)
    $target_user_agent = array_distinct($e.network.http.user_agent)
    $principal_ip = array_distinct($e.principal.ip)
    $principal_ip_country = array_distinct($e.principal.ip_geo_artifact.location.country_or_region)
    $principal_ip_state = array_distinct($e.principal.ip_geo_artifact.location.state)
    $principal_ip_city = array_distinct($e.principal.location.city)
    $principal_ip_carrier = array_distinct($e.principal.ip_geo_artifact.network.carrier_name)
    $security_result_summary = array_distinct($e.security_result.summary)
    $principal_user_managers_email_addresses = array_distinct($e.target.user.managers.email_addresses)
    $target_user_email_addresses = array_distinct($e.target.user.email_addresses)
    $target_user_userid = array_distinct($e.target.user.userid)
    $log_type = array_distinct($e.metadata.log_type)

  condition:
    $e and $event_count_days_seen >= 5 and $anomaly_score > 5 and $current_count > 10// and ($stddev_event_count_window > 90 OR $stddev_event_count_window < 1)
}
