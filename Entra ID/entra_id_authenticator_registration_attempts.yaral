rule entra_id_authenticator_registration_attempts {

  meta:
    author = "Jasoon Walker @tresscross"
    description = "Detects when an EntraID authentication registration is attempted, this rule will capture both success and failures."
    tactic = "Persistence"
    technique = "Account Manipulation"
    data_source = "EntraID"
    severity = "Low"
    priority = "Low"

  events:
    $e.metadata.product_name = "Azure AD Directory Audit" 
    $e.target.application = "Authentication Methods"  
    (
        $e.security_result.description = /user registered mobile phone sms/ nocase OR
        $e.security_result.description = /user started the registration for mobile phone sms/ nocase OR
        $e.security_result.description = /user started the registration for authenticator app with notification and code/ nocase OR
        $e.security_result.description = /User registered Outlook mobile with Code/ nocase
    )
    $e.principal.user.userid = $user
    $e.security_result.description = $attempt_type

  match:
    $user over 1h

  outcome:
    $risk_score = 30

    $principal_user_userid = array_distinct($e.principal.user.userid)
    $principal_ip = array_distinct($e.principal.ip)
    $principal_ip_country = array_distinct($e.principal.ip_geo_artifact.location.country_or_region)
    $principal_ip_state = array_distinct($e.principal.ip_geo_artifact.location.state)
    $principal_ip_city = array_distinct($e.principal.location.city)
    $principal_ip_carrier = array_distinct($e.principal.ip_geo_artifact.network.carrier_name)

    $action = array_distinct($e.security_result.description)

  condition:
    $e
}
