rule gcp_secret_manager_mass_deletion {
  meta:
    author = "Jason Walker @tresscross"
    description = "GCP Secret Manager Mass Deletion, this may be common behavior in dev, testing, or deprecated projects."
    severity = "Medium"

  events:
    $e.metadata.event_type = "RESOURCE_DELETION"
    $e.metadata.log_type = "GCP_CLOUDAUDIT"
    $e.metadata.product_event_type = "google.cloud.secretmanager.v1.SecretManagerService.DeleteSecret"
    $username = $e.principal.user.userid

  match:
    $username over 1h

  outcome:
    $risk_score = 35
    $user = array_distinct($e.principal.user.userid)
    $resource_count = count_distinct($e.target.resource.name)
    $project_path = array_distinct($e.principal.resource.attribute.cloud.project.name)
    $gcpproject = array_distinct($e.target.cloud.project.name)
    $ip = array_distinct($e.principal.ip)
    $principal_state = array_distinct($e.principal.ip_geo_artifact.location.state)
    $principal_country_or_region = array_distinct($e.principal.ip_geo_artifact.location.country_or_region)
    $principal_carrier_name = array_distinct($e .principal.ip_geo_artifact.network.carrier_name)
    $result = array_distinct($e.security_result.action)


  condition:
    $e and $resource_count > 10 

}
