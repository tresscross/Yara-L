rule slack_anomaly_unexpected_api_call_volume {

  meta:
    author = "Jason Walker @tresscross"
    description = "The unexpected API call volume detection generates anomalies when a user generates a larger volume of API calls than would be expected from a browser or official Slack client accessing Slack. This could indicate that a user is using a non-standard client or performing scraping activities."
    severity = "High"

  events:
    $e.metadata.log_type = "SLACK_AUDIT"
    $e.metadata.product_event_type = "anomaly"
    $e.security_result.description = /unexpected_api_call_volume/ nocase
    $user = $e.principal.user.userid

match:
    $user over 5m

  outcome:
    $p_ip = array_distinct($e.principal.ip)
    $p_user = array_distinct($e.target.user.userid)
    $target_state = array_distinct($e.principal.ip_geo_artifact.location.state)
    $target_country_or_region = array_distinct($e.principal.ip_geo_artifact.location.country_or_region)
    $target_asn = array_distinct($e.principal.ip_geo_artifact.network.asn)
    $target_carrier_name = array_distinct($e.principal.ip_geo_artifact.network.carrier_name)
    $reason = array_distinct($e.security_result.description)
    $result = array_distinct($e.security_result.action)
    $useragent = array_distinct($e.network.http.user_agent)
    $event_type = array_distinct($e.metadata.event_type)

  condition:
    $e
}
