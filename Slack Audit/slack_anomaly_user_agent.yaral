rule slack_anomaly_user_agent {

  meta:
    author = "Jason Walker @tresscross"
    description = "An anomaly was detected in the user agent used for the user token (ex: there was a downgrade in a version). The best way to investigate such an event is to look at ua and previous_ua in the audit logs, as these will illustrate the change in user_agent."
    severity = "Medium"

  events:
    $e.metadata.log_type = "SLACK_AUDIT"
    $e.metadata.product_event_type = "anomaly"
    $e.security_result.description = /user_agent/ nocase
    $user = $e.principal.user.userid

match:
    $user over 5m

  outcome:
    $p_ip = array_distinct($e.principal.ip)
    $p_user = array_distinct($e.target.user.userid)
    $target_state = array_distinct($e.principal.ip_geo_artifact.location.state)
    $target_country_or_region = array_distinct($e.principal.ip_geo_artifact.location.country_or_region)
    $target_asn = array_distinct($e.principal.ip_geo_artifact.network.asn)
    $target_carrier_name = array_distinct($e.principal.ip_geo_artifact.network.carrier_name)
    $reason = array_distinct($e.security_result.description)
    $result = array_distinct($e.security_result.action)
    $useragent = array_distinct($e.network.http.user_agent)
    $event_type = array_distinct($e.metadata.event_type)

  condition:
    $e
}
